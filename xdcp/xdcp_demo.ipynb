{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "This is a SageMath notebook that attempts to solve xDCP instances: given an elliptic curve $E/\\mathbb{F}_q$, an integer $k$ and a polynomial $f\\in\\mathbb{F}_q[x_1,x_2]$, find $P=(x_1,y_1), Q=(x_2,y_2) \\in E(\\mathbb{F}_q)$ such that $f(x_1,x_2)=0$ and $k*P = Q$. \n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {},
   "outputs": [],
   "source": [
    "# generate a curve of given size\n",
    "def gen_setup(bits):\n",
    "    q = next_prime(2 ** bits)\n",
    "    F = GF(q)\n",
    "    for b in F:\n",
    "        try:\n",
    "            E = EllipticCurve(F,[-3,b])\n",
    "        except ArithmeticError:\n",
    "            pass\n",
    "        if is_prime(E.order()) and E.order() != q:\n",
    "            break\n",
    "    n = E.order()\n",
    "    return q, E, n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 25,
   "metadata": {},
   "outputs": [],
   "source": [
    "#find l such that both l and k*l are small modulo q\n",
    "def find_l(k, q):\n",
    "    M = Matrix(ZZ,2)\n",
    "    M[0] = [1, k]\n",
    "    M[1] = [0, q]\n",
    "    return M.LLL()[0][0]\n",
    "\n",
    "def make_small(x, K):\n",
    "    return ZZ(min(K(x), -K(x)))\n",
    "\n",
    "#construct an auxilliary polynomial as a part of solving xDCP\n",
    "def construct_poly(E, F, n, l, k, order_multiple=0, reduceby=None):\n",
    "    R.<x1,x2> = PolynomialRing(F)\n",
    "    K = GF(n)\n",
    "    first = E.multiplication_by_m(make_small(l, K))[0](x=x1)\n",
    "    second = E.multiplication_by_m(ZZ(make_small(l*k, K)) + order_multiple * n)[0](x=x1)\n",
    "    if order_multiple > 0:\n",
    "        index = order_multiple * n - make_small(l*k, K)\n",
    "    else:\n",
    "        index = make_small(l*k, K)\n",
    "    if F(index) == 0:\n",
    "        index += n\n",
    "#     second = E.multiplication_by_m(index)[0](x=x1)\n",
    "#     print(first,second)\n",
    "    \n",
    "    if reduceby:\n",
    "        I = R.ideal(reduceby)\n",
    "        second_num = I.reduce(second.numerator())\n",
    "        second_den = I.reduce(second.denominator())\n",
    "        second = second_num / second_den\n",
    "        poly = (I.reduce(f(x1=first, x2=second).numerator()).univariate_polynomial())(x=x1)\n",
    "        \n",
    "    else:\n",
    "        poly = f(x1=first, x2=second).numerator().univariate_polynomial()\n",
    "        \n",
    "    return poly\n",
    "    \n",
    "def solve_xdcp(f, E, F, n, k, l, verbose=True):\n",
    "    \n",
    "    # catch multiplication_by_0\n",
    "    assert F(l*k) != 0, \"Please choose k,l with nonzero product mod q\"\n",
    "    assert l*k % n != 0, \"Please choose k,l with nonzero product mod n\"\n",
    "    \n",
    "    poly1 = construct_poly(E, F, n, l, k)\n",
    "    poly2 = construct_poly(E, F, n, l, k, order_multiple=1, reduceby=poly1)\n",
    "    if k == 1:\n",
    "        poly = poly1\n",
    "    else:\n",
    "        poly = gcd(poly1, poly2)\n",
    "    \n",
    "    # if the x-coord is valid, reconstruct the original point\n",
    "    roots = poly1.roots()\n",
    "    for r in roots:\n",
    "        x_coord = r[0]\n",
    "        if not E.is_x_coord(x_coord):\n",
    "            continue\n",
    "        R = E.lift_x(x_coord)\n",
    "        P = ZZ(l)*R\n",
    "        Q = ZZ(l*k)*R\n",
    "        assert f(P[0],Q[0]) == 0, \"The found points do not satisfy xdcp\"\n",
    "        if verbose:\n",
    "            print(f\"Points P = {P}, Q = {Q} satisfy xDCP for f = {f}, k = {k}, l = {l}\")\n",
    "        return P, Q\n",
    "    if verbose:\n",
    "        print(f\"No points P, Q satisfy xDCP for f = {f}, k = {k}\")\n",
    "    return None, None"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 26,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Points P = (31 : 25 : 1), Q = (31 : 25 : 1) satisfy xDCP for f = x1*x2 + 1, k = 1, l = 1\n",
      "Points P = (12 : 6 : 1), Q = (3 : 29 : 1) satisfy xDCP for f = x1*x2 + 1, k = 2, l = 1\n",
      "Points P = (32 : 26 : 1), Q = (15 : 34 : 1) satisfy xDCP for f = x1*x2 + 1, k = 3, l = 1\n",
      "No points P, Q satisfy xDCP for f = x1*x2 + 1, k = 4\n",
      "Points P = (36 : 23 : 1), Q = (1 : 9 : 1) satisfy xDCP for f = x1*x2 + 1, k = 5, l = 1\n",
      "No points P, Q satisfy xDCP for f = x1*x2 + 1, k = 6\n",
      "No points P, Q satisfy xDCP for f = x1*x2 + 1, k = 7\n",
      "Points P = (1 : 28 : 1), Q = (36 : 23 : 1) satisfy xDCP for f = x1*x2 + 1, k = 8, l = -5\n",
      "No points P, Q satisfy xDCP for f = x1*x2 + 1, k = 9\n",
      "No points P, Q satisfy xDCP for f = x1*x2 + 1, k = 10\n",
      "Points P = (35 : 9 : 1), Q = (19 : 15 : 1) satisfy xDCP for f = x1*x2 + 1, k = 11, l = -3\n",
      "No points P, Q satisfy xDCP for f = x1*x2 + 1, k = 12\n",
      "No points P, Q satisfy xDCP for f = x1*x2 + 1, k = 13\n",
      "Points P = (15 : 3 : 1), Q = (32 : 11 : 1) satisfy xDCP for f = x1*x2 + 1, k = 14, l = -3\n",
      "Points P = (19 : 22 : 1), Q = (35 : 28 : 1) satisfy xDCP for f = x1*x2 + 1, k = 15, l = 5\n",
      "No points P, Q satisfy xDCP for f = x1*x2 + 1, k = 16\n",
      "No points P, Q satisfy xDCP for f = x1*x2 + 1, k = 17\n",
      "No points P, Q satisfy xDCP for f = x1*x2 + 1, k = 18\n",
      "No points P, Q satisfy xDCP for f = x1*x2 + 1, k = 19\n",
      "Points P = (3 : 8 : 1), Q = (12 : 6 : 1) satisfy xDCP for f = x1*x2 + 1, k = 20, l = -2\n",
      "Points P = (3 : 8 : 1), Q = (12 : 31 : 1) satisfy xDCP for f = x1*x2 + 1, k = 21, l = -2\n",
      "No points P, Q satisfy xDCP for f = x1*x2 + 1, k = 22\n",
      "No points P, Q satisfy xDCP for f = x1*x2 + 1, k = 23\n",
      "No points P, Q satisfy xDCP for f = x1*x2 + 1, k = 24\n",
      "No points P, Q satisfy xDCP for f = x1*x2 + 1, k = 25\n",
      "Points P = (19 : 15 : 1), Q = (35 : 28 : 1) satisfy xDCP for f = x1*x2 + 1, k = 26, l = 3\n",
      "Points P = (15 : 3 : 1), Q = (32 : 26 : 1) satisfy xDCP for f = x1*x2 + 1, k = 27, l = 4\n",
      "No points P, Q satisfy xDCP for f = x1*x2 + 1, k = 28\n",
      "No points P, Q satisfy xDCP for f = x1*x2 + 1, k = 29\n",
      "Points P = (35 : 9 : 1), Q = (19 : 22 : 1) satisfy xDCP for f = x1*x2 + 1, k = 30, l = 5\n",
      "No points P, Q satisfy xDCP for f = x1*x2 + 1, k = 31\n",
      "No points P, Q satisfy xDCP for f = x1*x2 + 1, k = 32\n",
      "Points P = (1 : 28 : 1), Q = (36 : 14 : 1) satisfy xDCP for f = x1*x2 + 1, k = 33, l = -1\n",
      "No points P, Q satisfy xDCP for f = x1*x2 + 1, k = 34\n",
      "No points P, Q satisfy xDCP for f = x1*x2 + 1, k = 35\n",
      "Points P = (36 : 14 : 1), Q = (1 : 9 : 1) satisfy xDCP for f = x1*x2 + 1, k = 36, l = -1\n",
      "Points P = (32 : 11 : 1), Q = (15 : 34 : 1) satisfy xDCP for f = x1*x2 + 1, k = 38, l = -1\n",
      "Points P = (12 : 31 : 1), Q = (3 : 29 : 1) satisfy xDCP for f = x1*x2 + 1, k = 39, l = -1\n",
      "Points P = (31 : 12 : 1), Q = (31 : 25 : 1) satisfy xDCP for f = x1*x2 + 1, k = 40, l = -1\n"
     ]
    }
   ],
   "source": [
    "#test the attack\n",
    "\n",
    "q, E, n = gen_setup(5)\n",
    "F = GF(q)\n",
    "R.<x1,x2> = PolynomialRing(F)\n",
    "f = x1*x2 + 1\n",
    "for k in [1..n-1]:\n",
    "    l = find_l(k, q)\n",
    "    try:\n",
    "        solve_xdcp(f, E, F, n, k=k, l=l, verbose=True)\n",
    "    except:\n",
    "        pass"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "SageMath 9.2",
   "language": "sage",
   "name": "sagemath"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.8.5"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
